CONSTRUCT {
  # Submodel (SM)
  ?SM_iri a aas:Submodel ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:keys [
        a aas:Key ;
        aaskey:idType aaskeyt:IRI ;
        aaskey:value ?Property ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?Class ;
  .
}
WHERE {
  { ?Class a owl:Class } UNION { ?Class a rdfs:Class }

  {
    ?Property rdfs:range ?Class .

    FILTER EXISTS { ?Property rdfs:domain/mas4ai:hasInterface [] }
    FILTER NOT EXISTS { ?Class rdfs:subClassOf/^rdfs:range ?Property }
  } UNION {
    ?Class mas4ai:hasInterface [] #Submodel for main class (data type properties)
  }

  BIND(iri(concat( "http://mas4ai.eu/id/sm/template/", struuid() )) as ?SM_iri)
}