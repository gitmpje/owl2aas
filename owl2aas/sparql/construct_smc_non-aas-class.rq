CONSTRUCT {
  # Submodel Element Collection (SMC)
  ?SMC_iri a aas:SubmodelElementCollection ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:keys [
        a aas:Key ;
        aaskey:idType aaskeyt:IRI ;
        aaskey:value ?Class ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?Class ;
  .
}
WHERE {
  { ?Class a owl:Class } UNION { ?Class a rdfs:Class }

  FILTER EXISTS { [] rdfs:domain ?Class }
  FILTER EXISTS { [] rdfs:range ?Class }
  FILTER NOT EXISTS { ?Class rdfs:subClassOf*/mas4ai:hasInterface [] }
  FILTER NOT EXISTS {
    #exclude classes 'directly related to' the 'main' MAS4AI class
    ?Class ^rdfs:range/rdfs:domain/mas4ai:hasInterface [] .
  }

  BIND(iri(concat( "http://mas4ai.eu/id/smc/template/", struuid() )) as ?SMC_iri)
}