CONSTRUCT {
  # Submodel Element Collection (SMC)
  ?SMC_iri a aas:SubmodelElementCollection ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:type aasreft:GlobalReference ;
      aasref:keys [
        a aas:Key ;
        aaskey:type aaskeyt:GlobalReference ;
        aaskey:value ?ClassStr ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?Class, ?Property ;
    mas4ai:constructedByQuery "construct_smc_non-aas-class.rq" ;
  .
}
WHERE {
  ?Class a owl:Class .
  ?Property rdfs:range ?Class .

  FILTER EXISTS { [] rdfs:domain ?Class }
  FILTER NOT EXISTS { ?Property rdfs:range/mas4ai:hasInterface [] } #exclude reference element properties

  # Exclude property-class pairs directly connected to a 'main' AAS class via a cardinality one property
  MINUS {
    ?Property a owl:FunctionalProperty ;
      rdfs:domain/mas4ai:hasInterface [] ;
      rdfs:range ?Class .
  }

  BIND(strdt(str(?Class), xsd:string) AS ?ClassStr)
  BIND(iri(concat( "http://mas4ai.eu/id/smc/template/", struuid() )) as ?SMC_iri)
}