CONSTRUCT {
  # Submodel Element Collection (SMC)
  ?SMC_iri a aas:SubmodelElementCollection ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:keys [
        a aas:Key ;
        aaskey:idType aaskeyt:IRI ;
        aaskey:value ?Property ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?Property ;
    mas4ai:constructedByQuery "construct_smc_cardinality.rq" ;
  .
}
WHERE {
  {
    ?Property a owl:DataTypeProperty .
    [] a owl:Restriction ;
      owl:onProperty ?Property ;
      owl:maxQualifiedCardinality ?maxCardinality .
    FILTER( xsd:int(?maxCardinality) > 1 )
  } UNION {
    ?Property a owl:DataTypeProperty .
    # If owl:maxCardinality>1 or not defined then embed the property in an ElementCollection
    FILTER NOT EXISTS { 
      [] a owl:Restriction ;
        owl:onProperty ?Property ;
        owl:maxQualifiedCardinality ?maxCardinality .
    }
  }

  BIND(iri(concat( "http://mas4ai.eu/id/smc/template/", struuid() )) as ?SMC_iri)
}