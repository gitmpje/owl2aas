CONSTRUCT {
  # Submodel Element Collection (SMC)
  ?SMC_iri a aas:SubmodelElementCollection ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:keys [
        a aas:Key ;
        aaskey:idType aaskeyt:IRI ;
        aaskey:value ?Class ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?Class ;
  .
}
WHERE {
  {
    ?Class a owl:Class .
  } UNION {
    ?Class a rdfs:Class .
  }

  ?Property rdfs:range ?Class .

  FILTER EXISTS { [] rdfs:domain ?Class }
  FILTER NOT EXISTS { ?Class mas4ai:hasInterface [] }
  FILTER NOT EXISTS {
    #exclude classes 'directly related to' the 'main' MAS4AI class
    ?Property rdfs:domain/mas4ai:hasInterface [] ;
      rdfs:range ?Class .
  }

  BIND(iri(concat( "http://mas4ai.eu/id/smc/template/", struuid() )) as ?SMC_iri)
}