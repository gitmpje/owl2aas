CONSTRUCT {
  # Submodel Element Collection (SMC)
  ?SMC_iri a aas:SubmodelElementCollection ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:type aasreft:GlobalReference ;
      aasref:keys [
        a aas:Key ;
        aaskey:type aaskeyt:GlobalReference ;
        aaskey:value ?PropertyStr ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?Property ;
    mas4ai:constructedByQuery "construct_smc_cardinality.rq" ;
  .
}
WHERE {
  { ?Property a owl:DataTypeProperty }
  UNION
  { ?Property a owl:ObjectProperty }

  FILTER NOT EXISTS { ?Property a owl:FunctionalProperty } # Functional properties have a maximum cardinality of one

  BIND(strdt(str(?Property), xsd:string) AS ?PropertyStr)
  BIND(iri(concat( "http://mas4ai.eu/id/smc/template/", struuid() )) as ?SMC_iri)
}